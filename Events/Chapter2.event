
EventPointerTable(0xD,Chapter2)

ALIGN 4
Chapter2:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN PlayerUnits PlayerUnits
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

ALIGN 4
PlayerUnits:
UNIT Ostrava EphraimLord Ostrava Level(1,Ally,0) [3,17] 0x0 0x0 0x1 REDA3R15 NoItems NoAI
UNIT Zorua EphraimLord Ostrava Level(1,Ally,0) [4,17] 0x0 0x0 0x1 REDA4R16 NoItems NoAI
UNIT Ben EphraimLord Ostrava Level(1,Ally,0) [2,17] 0x0 0x0 0x1 REDA2R16 NoItems NoAI
UNIT Yanik EphraimLord Ostrava Level(1,Ally,0) [3,17] 0x0 0x0 0x1 REDA1R14 NoItems NoAI
UNIT Juliana EphraimLord Ostrava Level(1,Ally,0) [1,17] 0x0 0x0 0x1 REDA5R16 NoItems NoAI
UNIT 

ALIGN 4
EnemyUnits:
UNIT 0x8E Mercenary Bone Level(3,Enemy,1) [11,11] 0x0 0x0 0x0 0x1 [IronSword] AttackInRangeAI
UNIT 0x8E Brigand Bone Level(3,Enemy,1) [6,10] 0x0 0x0 0x0 0x1 [IronAxe] AttackInRangeAI
UNIT 0x8E Brigand Bone Level(3,Enemy,1) [6,7] 0x0 0x0 0x0 0x1 [HandAxe] AttackInRangeAI
UNIT 0x8E SwdCav Bone Level(2,Enemy,1) [1,8] 0x0 0x0 0x0 0x1 [IronSword] AttackInRangeAI
UNIT 0x8E AxeCav Bone Level(2,Enemy,1) [13,10] 0x0 0x0 0x0 0x1 [IronAxe] AttackInRangeAI
UNIT 0x8E Archer Bone Level(3,Enemy,1) [4,10] 0x0 0x0 0x0 0x1 [Longbow] AttackInRangeAI
UNIT 0x8E Mercenary Bone Level(3,Enemy,1) [5,5] 0x0 0x0 0x0 0x1 [SlimSword] AttackInRangeAI
UNIT 0x8E Mage Bone Level(2,Enemy,1) [10,6] 0x0 0x0 0x0 0x1 [ThunderTome] AttackInRangeAI
UNIT 0x8E Mage Bone Level(4,Enemy,1) [3,6] 0x0 0x0 0x0 0x1 [WindTome] AttackInRangeAI
UNIT 0x8E Mercenary Bone Level(4,Enemy,1) [9,9] 0x2 0x0 0x0 0x1 [IronBlade] AttackInRangeAI
UNIT 0x8E Brigand Bone Level(2,Enemy,1) [2,2] 0x0 0x0 0x0 0x1 [IronAxe] AttackInRangeAI
UNIT 0x8E Shaman Bone Level(2,Enemy,1) [14,6] 0x0 0x0 0x0 0x1 [FluxTome] AttackInRangeAI
UNIT

ALIGN 4
BossAppearsUnits:
UNIT Bone Brigand Bone Level(6,Enemy,0) [7,0] 0x0 0x0 0x0 0x1 [SteelAxe] NeverMoveAI
UNIT Elizabeth Thief_F Ostrava Level(4,Enemy,0) [8,0] 0x0 0x0 0x0 0x1 [IronSword,Lockpick] GuardTileAI
UNIT

ALIGN 4
ScaredyCat:
UNIT Roshal Archer Ostrava Level(10,NPC,0) [2,6] 0x0 0x0 3 RoshalREDAs [KillerBow,SteelBow,Vulnerary] NoAI
UNIT

ALIGN 4
RoshalREDAs:
REDA [2,10] 0x0 0x0 0x0 0x0
REDA [6,10] 0x0 0x0 0x0 0x0
REDA [6,13] 0x0 0x0 0x0 0x0

ALIGN 4
BallistaArcher:
UNIT 0x8E Archer Bone Level(2,Enemy,1) [14,17] 0x0 0x0 0x0 0x1 [IronBow] [0,3,9,0]
UNIT

ALIGN 4
TurnBasedEvents:
TurnEventEnemy(9,BossShowsUp,1)
TurnEventPlayer(15,ArcherAppears,2)
END_MAIN

ALIGN 4
CharacterBasedEvents:
CharacterEventBothWays(10,RecruitRoshal,Ostrava,Roshal)
CharacterEventBothWays(11,RecruitElizabeth,Juliana,Elizabeth)
END_MAIN

ALIGN 4
LocationBasedEvents:
Chest(HandAxe,3,0)
Chest(ThunderTome,14,0)
Door(12,4)
Door(2,4)
END_MAIN

ALIGN 4
MiscBasedEvents:
CauseGameOverIfLordDies
DefeatBoss(EndingScene)
END_MAIN

ALIGN 4
Dunno:
//DO NOT TOUCH
WORD $00

ALIGN 4
Tutorial:
//DO NOT TOUCH
WORD $00

ALIGN 4
TrapData:
NormalBallista(11,16)
END_MAIN

ALIGN 4
BeginningScene:
MUSC Advance
REVEAL Zo
REVEAL Ben
REVEAL Yanik
REVEAL Juli
CAM1 [6,13]
FADU 10
LOAD2 0x1 PlayerUnits
ENUN
Text(Chapter2Opening1)
MUSC Tension
LOAD1 0x1 ScaredyCat
ENUN
Text(Chapter2Opening2)
LOAD1 0x1 EnemyUnits
ENUN
Text(Chapter2Opening3)
STAL 5
NoFade
ENDA

ALIGN 4
EndingScene:
MUSC Into_the_Shadow_of_Victory
Text($28,Chapter2Ending1)
EVBIT_T 2
CHECK_ALLEGIANCE Elizabeth
SVAL 1 0
BNE 2 1 0xC
  CHECK_ALIVE Elizabeth
  SVAL 1 1
  BNE 2 1 0xC
    Text(28,Chapter2Ending2)
LABEL 2
EVBIT_T 2
MNC2 3
ENDA

ALIGN 4
BossShowsUp:
CAM1 [6,5]
LOAD1 0x1 BossAppearsUnits
Text(Chapter2BossAppears)
MOVE 0 Elizabeth [7,2]
ENUN
SOUN 0xB1
TILECHANGE 2
MOVE 0 Elizabeth [7,5]
ENUN
NoFade
ENDA

ALIGN 4
RecruitRoshal:
MUSS Song_TogetherWeRide
Text(Chapter2RoshalRecruitment)
TurnAlly(Roshal)
MURE 5
NoFade
ENDA

ALIGN 4
RecruitElizabeth:
MUSS Song_TogetherWeRide
Text(Chapter2ElizabethRecruitment)
TurnAlly(Elizabeth)
MURE 5
NoFade
ENDA

ALIGN 4
ArcherAppears:
LOAD1 0x1 BallistaArcher
ENUN
NoFade
ENDA








